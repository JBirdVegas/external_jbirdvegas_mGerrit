buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'org.apache.commons:commons-lang3:3.4'
        classpath 'org.ajoberstar:gradle-git:1.1.0'
    }
}

import com.jbirdvegas.mgerrit.build.utils.VersionHelper

apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'checkstyle'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile files('libs/libGoogleAnalyticsServices.jar')

    compile 'org.jetbrains:annotations:13.0'

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    compile 'com.google.code.gson:gson:2.6.1'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    compile 'com.nhaarman.listviewanimations:lib-core-slh:3.1.0@aar'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'joda-time:joda-time:2.9.2'
    compile 'org.ajoberstar:gradle-git:1.1.0'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile('com.github.dmytrodanylyk.android-process-button:library:1.0.3') {
        exclude module: 'support-v4'
    }
    compile 'de.hdodenhof:circleimageview:1.2.2'

/*  TODO: Enable this when the Android variant of the Gerrit Rest Java Client is officially released
       The corresponding jar can then be removed
    compile('com.urswolfer.gerrit.client.rest:gerrit-rest-java-client:0.8.6-android') {
        exclude module: 'httpclient'
    }*/
    compile files('libs/gerrit-rest-java-client-0.8.8-android.jar')
    // These are dependencies of the gerrit-rest-java-client library above
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'

    // We have too many methods including the appcompat library
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.anupcowkur:reservoir:2.1'

    compile('com.mikepenz:materialdrawer:5.0.0@aar') {
        transitive = true
        exclude module: 'appcompat-v7'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    // compile Using Java 7
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        // Enabling multidex support.
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }

    sourceSets {
        main {}
    }
    signingConfigs {
        release {}
    }
    buildTypes {
        release {
            minifyEnabled false
//            proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
        // With community translations we are always going to get these warnings
        disable 'MissingTranslation', 'ExtraTranslation'
    }
    packagingOptions {
        exclude 'plugin.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ECLIPSE_.RSA'
        exclude 'META-INF/ECLIPSE_.SF'
    }

    defaultConfig {
        VersionHelper helper = new VersionHelper(getProject())
        versionName helper.versionName
        versionCode helper.versionCode
        minSdkVersion 14
        targetSdkVersion 23
        println("versionCode: $versionCode versionName: $versionName")
    }
}

apply plugin: 'signing-plugin'
apply plugin: 'tasks-plugin'

/**
 * Task that's acts as an anchor for complete releases
 */
task fullRelease << {
    // just an anchor for complete release builds
    // Build script below: should run commit before build otherwise the resulting apk will not have the latest version number attached
    // gradle clean -PincrementVersion=true style commit build push
}

// config the style checks to run during the testing phase
//check.dependsOn 'checkstyle'
// Configure commit to only run after clean builds
commit.dependsOn 'clean', 'style', 'assembleRelease'
// Pushing requires having committed files and created tags
push.dependsOn 'commit'
// full release can only be successful if all pushes were successful
fullRelease.dependsOn 'push'
